# prepend this file with QCA_INCDIR/QCA_LIBDIR definitions

# NOTE: any changes made to this file need to be tracked in qcm/qca.qcm

CONFIG *= qt

# if we are including crypto.prf from the qca tree (and not utilizing it as
# an installed qmake CONFIG feature), then point to the tree.  this allows our
# qca tree apps to build before qca itself is installed.
exists($$PWD/qca.pro) {
	QCA_INCDIR = $$PWD/include
	QCA_LIBDIR = $$PWD/lib
}

LINKAGE =

# on mac, if qca was built as a framework, link against it
mac: {
	framework_dir = $$QCA_LIBDIR
	exists($$framework_dir/qca.framework) {
		#QMAKE_FRAMEWORKPATH *= $$framework_dir
		LIBS += -F$$framework_dir
		INCLUDEPATH += $$framework_dir/qca.framework/Headers
		LINKAGE = -framework qca
	}
}

# else, link normally
isEmpty(LINKAGE) {
	INCLUDEPATH += $$QCA_INCDIR/QtCrypto
	LIBS += -L$$QCA_LIBDIR
	LINKAGE = -lqca
	CONFIG(debug, debug|release) {
		windows:LINKAGE = -lqcad
		mac:LINKAGE = -lqca_debug
	}
}

LIBS += $$LINKAGE
